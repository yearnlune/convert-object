buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

plugins {
    id 'java'
}

apply plugin: 'com.jfrog.bintray'

def getGitRevision = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

group = 'yearnlune.lab'
version = "0.0.${getGitRevision()}"
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter:2.1.4.RELEASE'
    implementation 'org.springframework.data:spring-data-commons:2.1.4.RELEASE'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

Properties properties = new Properties()
File propFile = project.rootProject.file(System.properties['user.home'] + "/local.properties")
if (propFile.exists()) {
    properties.load(propFile.newDataInputStream())
}else{
    properties.put("artifactory.username", System.getenv("ARTIFACTORY_USERNAME"))
    properties.put("artifactory.password", System.getenv("ARTIFACTORY_PASSWORD"))
}

bintray {
    user = properties.getProperty("artifactory.username", "")
    key =  properties.getProperty("artifactory.password", "")
    pkg {
        repo = 'maven'
        name = rootProject.name
        userOrg = properties.getProperty("artifactory.username", "")
        vcsUrl = "${properties.getProperty("artifactory.github", "")}/${rootProject.name}.git"
        issueTrackerUrl = "${properties.getProperty("artifactory.github", "")}/${rootProject.name}/issues"
        version {
            name = "0.0.${getGitRevision()}"
        }
    }
}